extends Control

## Studio splash screen. Shows Wunderelf Studios branding, then auto-advances
## to the title screen. Press any key to skip.

const _TITLE_SCENE: String = "res://scenes/title/title.tscn"
const _LOGO_PATH: String = "res://assets/art/ui/wunderelf_logo.png"

const _INITIAL_DELAY: float = 0.5
const _LOGO_FADE_IN: float = 1.0
const _TEXT_FADE_IN: float = 0.5
const _HOLD_DURATION: float = 5.0

var _logo: TextureRect
var _studio_label: Label
var _transitioning: bool = false
var _skip_enabled: bool = false


func _ready() -> void:
	# Skip threaded preload in headless mode — the load thread races with --quit shutdown
	# and produces a spurious "Parse Error: Failed" during cleanup.
	if DisplayServer.get_name() != "headless":
		SceneManager.preload_scene(_TITLE_SCENE)
	_build_ui()
	_play_sequence()


func _build_ui() -> void:
	var center := CenterContainer.new()
	center.set_anchors_preset(Control.PRESET_FULL_RECT)
	add_child(center)

	var vbox := VBoxContainer.new()
	vbox.add_theme_constant_override("separation", 0)
	vbox.alignment = BoxContainer.ALIGNMENT_CENTER
	center.add_child(vbox)

	# Elf logo image
	if ResourceLoader.exists(_LOGO_PATH):
		_logo = TextureRect.new()
		_logo.texture = load(_LOGO_PATH)
		_logo.expand_mode = TextureRect.EXPAND_FIT_WIDTH_PROPORTIONAL
		_logo.stretch_mode = TextureRect.STRETCH_KEEP_ASPECT_CENTERED
		_logo.texture_filter = CanvasItem.TEXTURE_FILTER_LINEAR
		_logo.custom_minimum_size = Vector2(400, 400)
		_logo.modulate.a = 0.0
		vbox.add_child(_logo)

	# Studio name text
	_studio_label = Label.new()
	_studio_label.text = "WUNDERELF STUDIOS"
	_studio_label.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	_studio_label.add_theme_font_size_override("font_size", 42)
	_studio_label.add_theme_color_override("font_color", Color(0.92, 0.82, 0.55, 1.0))
	_studio_label.modulate.a = 0.0
	vbox.add_child(_studio_label)


func _play_sequence() -> void:
	var tween := create_tween()
	tween.tween_interval(_INITIAL_DELAY)
	tween.tween_callback(func() -> void: _skip_enabled = true)
	if _logo:
		tween.tween_property(_logo, "modulate:a", 1.0, _LOGO_FADE_IN)
	if _studio_label:
		tween.tween_property(_studio_label, "modulate:a", 1.0, _TEXT_FADE_IN)
	tween.tween_interval(_HOLD_DURATION)
	await tween.finished
	if not _transitioning:
		_go_to_title()


func _unhandled_input(_event: InputEvent) -> void:
	pass  # Skip disabled for trailer recording


func _go_to_title() -> void:
	if _transitioning:
		return
	_transitioning = true
	SceneManager.change_scene(_TITLE_SCENE)
